apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

dependencies {
    implementation fileTree(include: '*.jar', dir: 'libs')
    implementation project(':lib')
    implementation project(':shared')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'

    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation group: 'androidx.constraintlayout', name: 'constraintlayout', version: '2.1.3'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'

    def room_version = '2.4.1'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    implementation libs.okio
    implementation libs.okhttp
    implementation libs.okhttp.urlconnection

    implementation 'com.umeng.umsdk:common:9.4.0'// 必选
    implementation 'com.umeng.umsdk:asms:1.2.3'// 必选
    implementation 'com.umeng.umsdk:apm:1.4.0' // 错误分析升级为独立SDK，看crash数据请一定集成，可选

    debugRuntimeOnly 'com.squareup.leakcanary:leakcanary-android:2.8.1'

    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.4.0'
    // Compose Material Design
    implementation 'androidx.compose.material:material:1.1.0'
    // Animations
    implementation 'androidx.compose.animation:animation:1.1.0'
    // Tooling support (Previews, etc.)
    implementation 'androidx.compose.ui:ui-tooling:1.1.0'
    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.4.1'

    // When using a AppCompat theme
    def accompanist_version = "0.23.0"
    implementation "com.google.accompanist:accompanist-appcompat-theme:$accompanist_version"
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"

    implementation libs.koin.android
    implementation libs.moko.resources

    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.1.0'
    androidTestImplementation 'androidx.test:core:1.4.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

android {
    compileSdkVersion compile_sdk_version

    Properties p = new Properties()
    p.load(project.rootProject.file('local.properties').newDataInputStream())

    Calendar c = Calendar.getInstance()
    int year = c.get(Calendar.YEAR)
    int month = c.get(Calendar.MONTH) + 1
    int day = c.get(Calendar.DAY_OF_MONTH)
    int vc = year * 10000 + month * 100 + day
    String vn = (year - 2013) + "." + month + "." + day

    defaultConfig {
        applicationId "com.njust.helper"
        minSdkVersion min_sdk_version
        targetSdkVersion compile_sdk_version
        versionCode vc
        versionName vn
        resConfigs 'en', 'zh', 'zh-rCN', 'zh-rHK', 'zh-rTW'
        resValue "string", "testStuid", p.getProperty('testStuid')
        resValue "string", "testJwcPwd", p.getProperty('testJwcPwd')
        resValue "string", "testLibPwd", p.getProperty('testLibPwd')
        resValue "string", "umengAppKey", p.getProperty('umengAppKey')
    }

    signingConfigs {
        debug {
            keyAlias p.getProperty('keyAlias')
            keyPassword p.getProperty('keyPassword')
            storeFile file(p.getProperty('storeFilePath'))
            storePassword p.getProperty('storePassword')
        }
        release {
            keyAlias p.getProperty('keyAlias')
            keyPassword p.getProperty('keyPassword')
            storeFile file(p.getProperty('storeFilePath'))
            storePassword p.getProperty('storePassword')
        }
    }

    buildTypes {
        debug {
            buildConfigField "String", "BASE_URL", "\"${p.getProperty('localUrl')}\""
        }
        release {
            buildConfigField "String", "BASE_URL", '"http://hqzxzwb.jisxu.top/njust0909/"'
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-project.txt', 'kotlinx-serialization.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        dataBinding = true
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.1.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

dependencies {
    implementation fileTree(include: '*.jar', dir: 'libs')
    implementation project(':lib')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.preference:preference:1.0.0'
    implementation group: 'androidx.constraintlayout', name: 'constraintlayout', version: '1.1.3'

    implementation 'com.tencent.bugly:crashreport:2.6.5'

    implementation "androidx.room:room-runtime:2.0.0-rc01"
    kapt "androidx.room:room-compiler:2.0.0-rc01"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:3.11.0"
    debugRuntimeOnly 'com.amitshekhar.android:debug-db:1.0.3'

    implementation("com.squareup.moshi:moshi:1.8.0")
    kapt("com.squareup.moshi:moshi-kotlin-codegen:1.8.0")
}

android {
    compileSdkVersion compile_sdk_version
    dataBinding {
        enabled = true
    }

    Properties p = new Properties()
    p.load(project.rootProject.file('local.properties').newDataInputStream())

    Calendar c = Calendar.getInstance()
    int year = c.get(Calendar.YEAR)
    int month = c.get(Calendar.MONTH) + 1
    int day = c.get(Calendar.DAY_OF_MONTH)
    int vc = year * 10000 + month * 100 + day
    String vn = (year - 2013) + "." + month + "." + day

    defaultConfig {
        applicationId "com.njust.helper"
        minSdkVersion min_sdk_version
        targetSdkVersion compile_sdk_version
        versionCode vc
        versionName vn
        resConfigs 'en', 'zh', 'zh-rCN', 'zh-rHK', 'zh-rTW'
        buildConfigField "String", "BASE_URL", '"http://hqzxzwb.jisxu.top/njust0909/"'
        buildConfigField "String", "TENCENT_BUGLY_ID", "\"${p.getProperty('tencentBuglyId')}\""
        resValue "string", "testStuid", p.getProperty('testStuid')
        resValue "string", "testJwcPwd", p.getProperty('testJwcPwd')
        resValue "string", "testLibPwd", p.getProperty('testLibPwd')

        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        debug {
            keyAlias p.getProperty('keyAlias')
            keyPassword p.getProperty('keyPassword')
            storeFile file(p.getProperty('storeFilePath'))
            storePassword p.getProperty('storePassword')
        }
        release {
            keyAlias p.getProperty('keyAlias')
            keyPassword p.getProperty('keyPassword')
            storeFile file(p.getProperty('storeFilePath'))
            storePassword p.getProperty('storePassword')
        }
    }

    buildTypes {
        debug {
            buildConfigField "String", "BASE_URL", "\"${p.getProperty('localUrl')}\""
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-project.txt', 'moshi.pro'
            signingConfig signingConfigs.release
        }
    }

    dexOptions {
        preDexLibraries true
        javaMaxHeapSize "4g"
        dexInProcess = true
    }
}

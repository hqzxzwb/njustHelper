apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

dependencies {
    implementation fileTree(include: '*.jar', dir: 'libs')
    implementation project(':lib')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'

    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation group: 'androidx.constraintlayout', name: 'constraintlayout', version: '2.0.4'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'

    def room_version = '2.3.0'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    implementation "com.squareup.okhttp3:okhttp-urlconnection:4.2.1"

    def moshi_version = '1.12.0'
    implementation("com.squareup.moshi:moshi:$moshi_version")
    kapt("com.squareup.moshi:moshi-kotlin-codegen:$moshi_version")

    implementation "com.squareup.okio:okio:2.7.0"

    implementation 'com.google.firebase:firebase-core:19.0.0'
    implementation 'com.google.firebase:firebase-crashlytics-ktx:18.1.0'

    implementation 'com.umeng.umsdk:common:9.4.0'// 必选
    implementation 'com.umeng.umsdk:asms:1.2.3'// 必选
    implementation 'com.umeng.umsdk:apm:1.4.0' // 错误分析升级为独立SDK，看crash数据请一定集成，可选

    debugRuntimeOnly 'com.squareup.leakcanary:leakcanary-android:2.0-beta-2'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

android {
    compileSdkVersion compile_sdk_version

    Properties p = new Properties()
    p.load(project.rootProject.file('local.properties').newDataInputStream())

    Calendar c = Calendar.getInstance()
    int year = c.get(Calendar.YEAR)
    int month = c.get(Calendar.MONTH) + 1
    int day = c.get(Calendar.DAY_OF_MONTH)
    int vc = year * 10000 + month * 100 + day
    String vn = (year - 2013) + "." + month + "." + day

    defaultConfig {
        applicationId "com.njust.helper"
        minSdkVersion min_sdk_version
        targetSdkVersion compile_sdk_version
        versionCode vc
        versionName vn
        resConfigs 'en', 'zh', 'zh-rCN', 'zh-rHK', 'zh-rTW'
        resValue "string", "testStuid", p.getProperty('testStuid')
        resValue "string", "testJwcPwd", p.getProperty('testJwcPwd')
        resValue "string", "testLibPwd", p.getProperty('testLibPwd')
        resValue "string", "umengAppKey", p.getProperty('umengAppKey')
    }

    signingConfigs {
        debug {
            keyAlias p.getProperty('keyAlias')
            keyPassword p.getProperty('keyPassword')
            storeFile file(p.getProperty('storeFilePath'))
            storePassword p.getProperty('storePassword')
        }
        release {
            keyAlias p.getProperty('keyAlias')
            keyPassword p.getProperty('keyPassword')
            storeFile file(p.getProperty('storeFilePath'))
            storePassword p.getProperty('storePassword')
        }
    }

    buildTypes {
        debug {
            buildConfigField "String", "BASE_URL", "\"${p.getProperty('localUrl')}\""
        }
        release {
            buildConfigField "String", "BASE_URL", '"http://hqzxzwb.jisxu.top/njust0909/"'
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-project.txt', 'moshi.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    dexOptions {
        preDexLibraries true
        javaMaxHeapSize "4g"
        dexInProcess = true
    }

    buildFeatures {
        dataBinding = true
    }
}
